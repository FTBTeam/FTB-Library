plugins {
	id "architectury-plugin" version "3.4-SNAPSHOT"
	id "dev.architectury.loom" version "1.6-SNAPSHOT" apply false
}

architectury {
	minecraft = rootProject.minecraft_version
}

subprojects {
	apply plugin: "dev.architectury.loom"

	loom {
		silentMojangMappingsLicense()
	}

	dependencies {
		minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
		mappings loom.officialMojangMappings()
	}
}

allprojects {
	apply plugin: "java"
	apply plugin: "architectury-plugin"
	apply plugin: "maven-publish"
	apply from: "https://raw.githubusercontent.com/FTBTeam/mods-meta/main/gradle/git-md-changelog.gradle"

	def ENV = System.getenv()
	version = "${mod_version}-build.${ENV.GITHUB_RUN_NUMBER ?: 9999}"
	group = project.maven_group
	archivesBaseName = project.archives_base_name

	sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = 17

	compileJava {
		options.encoding = "UTF-8"
	}

	repositories {
		mavenCentral()

		maven {
			url "https://maven.architectury.dev/"
		}

		maven {
			url "https://www.cursemaven.com"
			content {
				includeGroup "curse.maven"
			}
		}

		maven {
			url "https://maven.saps.dev/minecraft"
			content {
				includeGroup "dev.latvian.mods"
				includeGroup "dev.ftb.mods"
			}
		}
	}

	dependencies {
		testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
	}

	java {
		withSourcesJar()
	}
}

task curseforgePublish
